buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
group = "mffs"
archivesBaseName = "Modular-Force-Field-System"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
	
	replaceIn "ModularForceFieldSystem.java"
	replace "@MAJOR@", config.version.mod.major
	replace "@MINOR@", config.version.mod.minor
	replace "@REVIS@", config.version.mod.revis
	replace "@BUILD@", System.getenv("BUILD_NUMBER")
}

if (System.getenv("BUILD_NUMBER") != null)
    version += ".${System.getenv("BUILD_NUMBER")}"


processResources {
	from 'build.properties'
}

task copyBuildXml(type: Copy) {
    from 'build.properties'
    into 'output'
}

task apiZip(type: Zip) {
    classifier = 'api'
    from sourceSets*.allSource
    include 'mffs/api/**'
    destinationDir = file 'output'
}

artifacts {
    archives apiZip
}

jar {
    dependsOn copyBuildXml
    classifier = ''
    destinationDir = file 'output'
	include('mffs/**')
	include('assets/**')
	include('*.png')
}

repositories {
    maven { url 'http://calclavia.com/maven/' }
}
dependencies {
    compile 'universalelectricity:Universal-Electricity:+:dev'
    compile 'calclaviacore:calclavia-core:+:dev'
}
